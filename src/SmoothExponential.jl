# @license BSD-3 https://opensource.org/licenses/BSD-3-Clause
# Copyright (c) 2022, Institute of Automatic Control - RWTH Aachen University
# All rights reserved. 

"""
    SmoothExponential(min, max, Œ≤, œÉ)
Smooth truncated exponential distribution by convolving the exponential with a normal distribution: Smooth = Exp ‚ãÜ Normal
This results in smooth min and max limits and a definition on ‚Ñù instead of ‚Ñù‚Å∫

Does not support `truncated` of Distributions.jl since it is a smooth truncation of the exponential distribution.
"""
struct SmoothExponential{T<:Real} <: AbstractKernelDistribution{T,Continuous}
    min::T
    max::T
    Œ≤::T
    œÉ::T
end

Base.show(io::IO, dist::SmoothExponential{T}) where {T} = print(io, "SmoothExponential{$(T)}, min: $(dist.min), max: $(dist.max), Œ≤: $(dist.Œ≤), œÉ: $(dist.œÉ)")

# Accurate version uses lower and upper bound
accurate_normalization(d::SmoothExponential) = -logsubexp(-d.min / d.Œ≤, -d.max / d.Œ≤)
accurate_factor(d::SmoothExponential, x) = (-x / d.Œ≤ + (d.œÉ / d.Œ≤)^2 / 2) - log(d.Œ≤) + accurate_normalization(d) + loghalf
function accurate_logerf(d::SmoothExponential{T}, x) where {T}
    invsqrt2œÉ = inv(my_sqrt2(T) * d.œÉ)
    lower = (d.min + d.œÉ^2 / d.Œ≤ - x) * invsqrt2œÉ
    upper = (d.max + d.œÉ^2 / d.Œ≤ - x) * invsqrt2œÉ
    my_logerf(lower, upper)
end

# Re-implementation of LogExpFunctions logerf which does not work with CUDA & Julia > 1.9 https://github.com/JuliaGPU/GPUCompiler.jl/issues/384 
function my_logerf(a::T, b::T) where {T<:Real}
    if abs(a) ‚â§ my_invsqrt2(T) && abs(b) ‚â§ my_invsqrt2(T)
        return log(erf(a, b))
    elseif b > a > 0
        return logerfc(a) + log1mexp(logerfc(b) - logerfc(a))
    elseif a < b < 0
        return logerfc(-b) + LogExpFunctions.log1mexp(logerfc(-a) - logerfc(-b))
    else
        return log(erf(a, b))
    end
end
my_sqrt2(::Type{T}) where {T<:Real} = T(sqrt2)
my_invsqrt2(::Type{T}) where {T<:Real} = T(invsqrt2)

# See my (Tim Redick) dissertation for the derivation.
Distributions.logpdf(dist::SmoothExponential{T}, x) where {T} = insupport(dist, x) ? accurate_factor(dist, x) + accurate_logerf(dist, x) : typemin(T)

# Exponential convoluted with normal: Sample from exponential and then add noise of normal
function rand_kernel(rng::AbstractRNG, dist::SmoothExponential{T}) where {T}
    # Distributions.jl truncated this fails to compile on RTX3080 so use naive implementation
    Œº = rand(rng, KernelExponential(dist.Œ≤)) + dist.min
    # BUG on RTX 3080 sometimes samples forever
    # while Œº > dist.max
    #     Œº = rand(rng, KernelExponential(dist.Œ≤)) + dist.min
    # end
    rand(rng, KernelNormal(Œº, dist.œÉ))
end

# Compared to a regular exponential distribution, this one is defined on ‚Ñù üòÉ
Base.maximum(::SmoothExponential{T}) where {T} = typemax(T)
Base.minimum(::SmoothExponential{T}) where {T} = typemin(T)
Bijectors.bijector(::SmoothExponential) = ZeroIdentity()
# Numerical issues if min‚âàmax. Return limit
Distributions.insupport(dist::SmoothExponential{T}, x::Real) where {T} = abs(dist.max - dist.min) < eps(T) ? false : true
